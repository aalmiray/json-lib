/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2006-2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.guide'   version '0.29.0'
    id 'org.ajoberstar.git-publish' version '0.3.2'
}

configurations {
    asciidoctorExtensions
}

dependencies {
    asciidoctorExtensions 'com.bmuschko:asciidoctorj-tabbed-code-extension:0.2'
}

asciidoctor {
    configurations 'asciidoctorExtensions'
    attributes = [
        'gradle-version'       : project.gradle.gradleVersion,
        'source-highlighter'   : 'prettify'
    ]
}

afterEvaluate {
    project.tasks.findByName('createGuide')

    gitPublish {
        repoUri = rootProject.extensions.findByName('config').info.links.scm
        branch  = 'gh-pages'
        contents {
            from project.tasks.findByName('createGuide').outputs.files
        }
        commitMessage = "Publish guide for $version"
    }

    gitPublishCommit.dependsOn(project.tasks.findByName('createGuide'))
}

gradle.addBuildListener(new BuildAdapter(){
    @Override
    void projectsEvaluated(Gradle gradle) {
        def createGuideTask = project.tasks.findByName('createGuide')

        def aggregateSourceXrefTask = rootProject.tasks.findByName('aggregateSourceXref')

        createGuideTask.finalizedBy project.tasks.create('copyAggregateXref', Copy) {
            dependsOn aggregateSourceXrefTask
            destinationDir = project.file("${createGuideTask.destinationDir}/api-xref")
            from aggregateSourceXrefTask.outputDirectory
        }
    }
})
